// Original concept by sandbox.
// re-vised by Mabuhay
// map i used is poring_w01, change it to any of your desired map
// the current map will have conflict with poring war event.
// edit as you wish.


-	script	Poring_Catcher	-1,{
OnInit:
	// item reward
	setarray .item,
		3105, 2;

	// @poringstart - gm manual start
	bindatcmd "poringstart", strnpcinfo(0)+"::OnStart",60,60;
	
	// @poring - player join event
	bindatcmd "poring", strnpcinfo(0)+"::OnJoinEvent";
	
	// Event cooldown
	.event_cd = 60 * 60 * 1; // 1hr
	
	// Mapflags
	setarray .@mapflag,
		mf_nowarp,
		mf_nowarpto,
		mf_noskill,
		mf_noloot,
		mf_noteleport,
		mf_nomemo,
		mf_nosave,
		mf_noicewall,
		mf_nobranch,
		mf_noexp,
		mf_noreturn;
		
	for ( .@i = 0; .@i < getarraysize(.@mapflag); .@i++)
		setmapflag "poring_w01", .@mapflag[.@i];
		
		
	.@min = gettime(DT_MINUTE)*60;
	.@sec = gettime(DT_SECOND);
	.event_time = (gettimetick(2) - ( .@min + .@sec )) + .event_cd - 5;
	end;
	
OnJoinEvent:
	mes " [ Event Status ] ";
	if ( !.start ) mes "Next schedule starts in ^FF0000"+ Time2Str(.event_time) +"^000000.";
	if ( .start == 1 ) mes "Event is currently active.";
	if ( .start == 2 ) mes "Event is currently on-going.";
	next;
	switch(select(
		"Poring Catcher - "+ ((.start == 1) ? "^00ff00Join Event":((.start == 2) ? "^00ff00Event On-Going":"^ff0000Not Available"))+"^000000" )) {
	case 1:
		if ( !.start ) {
			mes "Event is not available.";
			close;
		} else if ( .start == 2 ) {
			mes "Event is on-going";
			close;
		}
		else
			warp "poring_w01",100,100;
	}
	end;

OnStart: .@force = true;
// Add your clock timers here.
OnClock0800:	// PORING
//OnClock1000:	// DICE
//OnClock1200:	// DISGUISE
//OnClock1400:	// CLUCKERS
//OnClock1600:	// BOMBRING
OnClock1800:	// PORING
//OnClock2000:	// DICE
//OnClock2200:	// DISGUISE
//OnClock0000:	// CLUCKERS
//OnClock0200:	// BOMBRING
	.@time = gettimetick(2);
	.@min = gettime(DT_MINUTE)*60;
	.@sec = gettime(DT_SECOND);
	if ( .event_time > .@time && !.@force || .start && !.@force ) end; // doesnt start if cd isn't done.
	.event_time = (.@time - ( .@min + .@sec )) + .event_cd - 5; 
	.start = 1;
	announce "Poring Catcher: We are going to have a Poring Catcher event.",0;
	sleep 10000;
	announce "Poring Catcher: For those who wants to join, use @poring",0;
	sleep 10000;
	announce "Poring Catcher: After 1 Minute Event will close.",0;
	sleep 10000;
	announce "Poring Catcher: So please if you want to join. use @poring",0;
	sleep 60000;
	announce "Poring Catcher: The entry to the event is now closed.",bc_all,0x0ceedb;
	.start = 2;
	sleep 3500;
	if( !getmapusers("poring_w01") ) {
		announce "Poring Catcher: Not enough participants in the Event.",bc_all,0x0ceedb;
		sleep 2500;
		announce "Poring Catcher: Event will be halted.",bc_all,0x0ceedb;
		.start = false;
		end;
	}
	mapannounce "poring_w01","We're about to begin the event.",bc_map,0x0ceedb;
	sleep 2500;
	mapannounce "poring_w01","The rules are simple.",bc_map,0x0ceedb;
	sleep 2500;
	mapannounce "poring_w01","In a few seconds I'm going to spawn tons of porings..",bc_map,0x0ceedb;
	sleep 2500;
	mapannounce "poring_w01","..All you need to do is kill the Poring with the right name..",bc_map,0x0ceedb;
	sleep 2500;
	mapannounce "poring_w01","..Which would be Poring.",bc_map,0x0ceedb;
	sleep 2000;
	mapannounce "poring_w01","If you kill the wrong one, you're out!",bc_map,0x0ceedb;
	sleep 2000;
	mapannounce "poring_w01","That's all. Let's begin.",bc_map,0x0ceedb;
	sleep 3000;
	areamonster "poring_w01",84,119,124,90,"Poring",1002,1,strnpcinfo(0)+"::OnMobKilledWin";
	areamonster "poring_w01",84,119,124,90,"Poking",1002,3*getmapusers("poring_w01"),strnpcinfo(0)+"::OnMobKilled";
	areamonster "poring_w01",84,119,124,90,"Poiring",1002,3*getmapusers("poring_w01"),strnpcinfo(0)+"::OnMobKilled";
	areamonster "poring_w01",84,119,124,90,"Poling",1002,3*getmapusers("poring_w01"),strnpcinfo(0)+"::OnMobKilled";
	areamonster "poring_w01",84,119,124,90,"Porng",1002,3*getmapusers("poring_w01"),strnpcinfo(0)+"::OnMobKilled";
	areamonster "poring_w01",84,119,124,90,"pH0ur1n6",1002,3*getmapusers("poring_w01"),strnpcinfo(0)+"::OnMobKilled";
	areamonster "poring_w01",84,119,124,90,"P0ring",1002,3*getmapusers("poring_w01"),strnpcinfo(0)+"::OnMobKilled";
	areamonster "poring_w01",84,119,124,90,"Porong",1002,3*getmapusers("poring_w01"),strnpcinfo(0)+"::OnMobKilled";
	areamonster "poring_w01",84,119,124,90,"Powing",1002,3*getmapusers("poring_w01"),strnpcinfo(0)+"::OnMobKilled";
	areamonster "poring_w01",84,119,124,90,"Poriing",1002,3*getmapusers("poring_w01"),strnpcinfo(0)+"::OnMobKilled";
	areamonster "poring_w01",84,119,124,90,"Boring",1002,3*getmapusers("poring_w01"),strnpcinfo(0)+"::OnMobKilled";
	areamonster "poring_w01",84,119,124,90,"Lolwut!?",1002,3*getmapusers("poring_w01"),"::OnMobKilled";
	areamonster "poring_w01",84,119,124,90,"Kill me to win!",1002,3*getmapusers("poring_w01"),strnpcinfo(0)+"::OnMobKilled";
	end;

OnMobKilled:
	dispbottom "You killed the wrong Poring! Yerrrrrrrrrrrr out!";
	warp "prontera",rand(148,163),rand(167,186);
	sleep 5000;
	if ( !getmapusers("poring_w01") ) {
		.start = false;
		killmonsterall "poring_w01";
		announce "Poring Catcher: No one won the Poring Catcher Event!.",bc_all,0x0ceedb;
	}
	end;
	
OnMobKilledWin:
	announce strcharinfo(0)+" won the Poring Catcher Event!",bc_all,0xFF7F50;
	.@amt = getarraysize(.item);
	for ( .@j = 0; .@j < .@amt; .@j += 2 )
		getitem .item[.@j], .item[.@j+1];
	// WEEKLY QUEST			
	.@selectIfCharExistQuery$ = "SELECT account_id FROM weeklyquest WHERE account_id = '"+getcharid(3)+"';";
		if (query_sql(.@selectIfCharExistQuery$, .@accid) > 0) {
			.@updaterecapQuery$ = "UPDATE `weeklyquest` SET `task9` = `task9` + 1 WHERE `account_id` = '"+getcharid(3)+"';";
		} else {
			.@updaterecapQuery$ = "INSERT INTO weeklyquest (`account_id` , `task9`) VALUES ('"+getcharid(3)+"','1');";
	}
		query_sql(.@updaterecapQuery$);
// END
	killmonsterall "poring_w01";
	sleep 5000;
	mapwarp "poring_w01","prontera",rand(148,163),rand(167,186);
	.start = false;
	end;	
}

poring_w01,105,123,3	script	Back to Prontera	4_BULLETIN_BOARD2,{
	warp "prontera",rand(148,163),rand(167,186);
	end;
}